// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // x value
    Mux16(a = x, b[0..15] = false, sel = zx, out = ZX);
    Not16(in = ZX, out = NotZX);
    Mux16(a = ZX, b = NotZX, sel = nx, out = NZX);
    // y value
    Mux16(a = y, b[0..15] = false, sel = zy, out = ZY);
    Not16(in = ZY, out = NotZY);
    Mux16(a = ZY, b = NotZY, sel = ny, out = NZY);
    // bitwise or arithmetic
    And16(a = NZX, b = NZY, out = BR);
    Add16(a = NZX, b = NZY, out = AR);
    Mux16(a = BR, b = AR, sel = f, out = R);
    // revert result or not
    Not16(in = R, out = NotR);
    Mux16(a = R, b = NotR, sel = no, out = out, out[0..7] = LR, out[8..15] = HR, out[15] = ng);

    // If result is zero
    Or8Way(in = LR, out = LNZR);
    Or8Way(in = HR, out = HNZR);
    Or(a = LNZR, b = HNZR, out = NZR);
    Not(in = NZR, out = zr);
}