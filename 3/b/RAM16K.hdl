// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/b/RAM16K.hdl
/**
 * Memory of 16K 16-bit registers.
 * If load is asserted, the value of the register selected by
 * address is set to in; Otherwise, the value does not change.
 * The value of the selected register is emitted by out.
 */
CHIP RAM16K {
    IN in[16], load, address[14];
    OUT out[16];

    PARTS:
    DMux4Way(
        in = load, sel = address[12..13],
        a = L0To4K,
        b = L4KTo8K,
        c = L8KTo12K,
        d = L12KTo16K);
    RAM4K(in = in, load = L0To4K,    address = address[0..11], out = DATA0To4K);
    RAM4K(in = in, load = L4KTo8K,   address = address[0..11], out = DATA4KTo8K);
    RAM4K(in = in, load = L8KTo12K,  address = address[0..11], out = DATA8KTo12K);
    RAM4K(in = in, load = L12KTo16K, address = address[0..11], out = DATA12KTo16K);
    Mux4Way16(
        a = DATA0To4K, 
        b = DATA4KTo8K, 
        c = DATA8KTo12K, 
        d = DATA12KTo16K,
        sel = address[12..13], out = out);
}
